package Uther_R_Redemption

import codeDamageTypings
import ClosureEvents
import MyAbilityIds
import HashSet
import MyBuffIds
import Abilities
import DamageEvent

public var holyPower = 0
let holyAllies = new HashSet<int>

public function redemptionHoly()
    DamageEvent.addListener(2) ->
        let src = DamageEvent.getSource()
        if src.isIllusion()
            skip
        else
            if (DamageEvent.getType() == ATTACK)
                let damtyp = DamageEvent.getDamageType()
                if (damtyp == DAMAGE_TYPE_NORMAL or damtyp == DAMAGE_TYPE_MAGIC)
                    let lvl = src.getAbilityLevel(UTHER_ABILITY_4)
                    if (lvl > 0 and GetRandomInt(0, 100) < 5 * lvl)
                        let targ = DamageEvent.getTarget()
                        let str = src.getStr(true)
                        let amount = str * 0.5 + 75 * lvl
                        let heal = str * lvl * 0.2
                        flashEffect(Abilities.holyBoltSpecialArt, targ.getPos())
                        src.damageTargetCode(amount, targ, null, -1, damageType.PHYSICALMELEE)
                        src.addHP(heal)
                        addHolyPower((lvl*0.25).ceil())

public function addHolyPower(int addHoly)
    holyPower = holyPower + addHoly
    if holyPower > 150
        holyPower = 150
    updateHolyPower()

public function removeHolyPower(int removeHoly)
    holyPower = holyPower - removeHoly
    updateHolyPower()

public function setHolyPower(int setHoly)
    holyPower = setHoly
    updateHolyPower()

function updateHolyPower()
    for int i = 0 to 8
        let tooltip = "Uther has a " + (5+5*(i+1)).toString() + " % on hit to deal 0.5 * str + " + (75 * (i+1)).toString() + " damage and heal for " + ((i+1) * 0.2).toString() + " * str. Gets " + (i+1).toString() + " holy power. Use buff on up to 3 allies to make their skill casts give you 1 holy power. Stores to a max of 150. Current amount: " + holyPower.toString()
        setAbilityExtendedTooltip(UTHER_ABILITY_4, tooltip, i)

public function redemptionAlly()
    EventListener.onTargetCast(UTHER_ABILITY_4) (unit caster, unit target) ->
        if holyAllies.size() > 2
            printTimedToPlayer("You got too many allies blessed", 5, caster.getOwner())
            target.removeAbility(UTHER_BUFF_1)
        else
            holyAllies.add(target.getTypeId())

    EventListener.add(EVENT_PLAYER_UNIT_DEATH) -> 
        let uid = GetDyingUnit().getTypeId()
        if holyAllies.has(uid)
            holyAllies.remove(uid)

    EventListener.add(EVENT_PLAYER_UNIT_SPELL_CAST) -> 
        let uid = GetTriggerUnit().getTypeId()
        if GetTriggerUnit().hasAbility(UTHER_BUFF_1) == false and holyAllies.has(uid)
            holyAllies.remove(uid)
        if holyAllies.has(uid)
            addHolyPower(1)